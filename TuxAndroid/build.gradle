buildscript {
    repositories {
        maven { url = 'https://maven.moddingx.org' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.moddingx:LegacyJavaConverter:1.2.0'
    }
}

plugins {
    id 'com.android.application' version '8.1.2'
}

apply plugin: 'org.moddingx.ljc'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    google()
}

android {
    namespace 'tuxcalculator.android'
    compileSdk 34

    defaultConfig {
        applicationId "tuxcalculator.android"
        minSdk 26
        targetSdk 34
        versionCode project.version.split('\\.').inject(0) { r,s -> 1000*r+s.toInteger() }
        versionName project.version.toString()
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    buildTypes {
        release {
            // Sign the release APK with the debug key
            signingConfig signingConfigs.debug
        }
    }
}

configurations {
    legacy
}

dependencies {
    legacy project(':core')
}

task convertDependencies(type: org.moddingx.ljc.gradle.LjcConfigurationTask) {
    project(':core').afterEvaluate {
        dependsOn project(':core').jar
    }
    input configurations.legacy
    languageLevel = 17
    project(':core').afterEvaluate {
        inputs.file(project(':core').jar.archiveFile)
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation convertDependencies.output()
}
